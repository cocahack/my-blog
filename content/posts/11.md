---
title: Gradle Java 프로젝트의 테스트 분리 방법
date: "2021-09-25T07:28:44Z"
template: "post"
draft: false
slug: "11"
category: "Gradle"
tags:
  - "Gradle"
description: "Gradle을 빌드 툴로 사용하는 Java 프로젝트에서, Unit Test와 Integration Test, Fixture 등을 분리하는 방법을 알아본다"
---

> Gradle 버전 *7.1.1*을 사용했습니다.

## Gradle 프로젝트의 기본 구조

Gradle로 Java 프로젝트를 구성할 때는 `plugins`에 `java` 또는 `java-library` 플러그인을 넣어줘야 한다.

> 각 플러그인에 대한 자세한 내용은 아래 문서 참조  
> [Java plugin](https://docs.gradle.org/current/userguide/building_java_projects.html)  
> [Java-library plugin](https://docs.gradle.org/current/userguide/java_library_plugin.html#java_library_plugin)

```groovy
plugins {
  id 'java'
}
```

이렇게 플러그인을 사용하면 `compileJava`, `test` 등 프로젝트에 필요한 기본 *task*들과 `SourceSets` 이 자동으로 설정된다.

`SourceSets` 에 따른 프로젝트 구조는 다음과 같다.

```text
.
├── build.gradle
├── gradle
│   └── wrapper
│       ├── gradle-wrapper.jar
│       └── gradle-wrapper.properties
├── gradlew
├── gradlew.bat
├── settings.gradle
└── src
    ├── main
    │   ├── java
    │   └── resources
    └── test
        ├── java
        └── resources
```

애플리케이션을 작성할 때는 `src/main` 디렉토리를 사용하고, 테스트를 작성할 때는 `src/test` 디렉토리를 사용하게 된다.

## Fixture 사용

테스트를 작성할 때 임의의 데이터를 만들어놓고 재사용하는 경우가 상당히 있다. 이런 성격의 코드들을 `src/test`에 테스트 클래스들과 같이 놔두어도 상관없긴 하지만,
좀 더 깔끔하게 프로젝트를 구성하고 싶다면 다른 곳에 관리하는 방법을 사용할 수도 있을 것이다.

이럴 때 사용하는 것이 `java-test-fixtures` 플러그인이다. 간단히 `plugins` 항목에 추가하기만 하면 사용할 수 있다.

```groovy
plugins {
    id 'java'
    id 'java-test-fixtures'
}
```

플러그인을 추가하고 빌드하면 `src/testFixtures` 디렉토리가 자동으로 source set으로 등록된다.

이 디렉토리는 `main` source set에 위치한 클래스들을 볼 수 있고, `test` source set은 작성한 fixtures 들을 볼 수 있다.

### 다른 모듈에서 Test Fixture 참조하기

Gradle 프로젝트가 여러 개의 모듈로 나뉘어 있다면, 다른 모듈의 fixture를 참조하고 싶을 때가 있을 것이다. 이럴때는 `dependencies` 항목에
아래와 같이 설정하면 된다.

```groovy
dependencies {
    implementation(project(":lib"))

    testImplementation 'junit:junit:4.13'
    testImplementation(testFixtures(project(":lib")))
}
```

## Integration Test 분리하기

`src/test` source set은 unit test를 위한 디렉토리로 사용하고, integration test는 다른 위치에 두면서 task도 분리하여 테스트를 분리할 수 있다.

Integration test를 위한 디렉토리로 `src/integrationTest` 를 쓴다고 가정해보자.

해야할 작업은 `src/integrationTest` 디렉토리를 gradle이 source set으로 인식하게 만드는 것과, integration test 클래스를 위한 빌드 및 테스트 실행 태스크를 만드는 것이다.

`build.gradle` 에 아래 코드를 삽입한다.

```groovy
// source set 추가
def integrationTest = sourceSets.create("integrationTest")

// Task 추가
def integrationTestTask = tasks.register("integrationTest", Test) {
    description = 'Runs the integration tests.'
    group = "verification"
    testClassesDirs = integrationTest.output.classesDirs
    classpath = integrationTest.runtimeClasspath
    mustRunAfter(tasks.named('test'))
}
tasks.named('check') {
    dependsOn(integrationTestTask)
}

// Integration Test이 JUnit으로 실행될 수 있도록 코드 추가
tasks.withType(Test) {
    useJUnitPlatform()
    testLogging {
        events = ["passed", "failed", "skipped"]
        exceptionFormat = "full"
    }
}
```

이렇게 작성하면 `src/integrationTest` 디렉토리 아래 `java`(혹은 spock를 사용한다면 `groovy`) 디렉토리를 만들고 테스트 클래스를 작성하는 방법과
비슷하게 패키지 이름에 따른 디렉토리를 만들고 그 아래에 integration test 클래스를 작성하면 된다.

테스트에 필요한 의존성을 추가할 때는 `integrationTestImplementation` 을 쓰면 된다.

```groovy
dependencies {
    integrationTestImplementation 'org.springframework.boot:spring-boot-starter-test'
    integrationTestImplementation 'org.mybatis.spring.boot:mybatis-spring-boot-starter-test:2.2.0'
    integrationTestImplementation 'org.springframework.restdocs:spring-restdocs-restassured'
    integrationTestImplementation 'org.testcontainers:mysql'
    integrationTestImplementation 'org.testcontainers:spock:1.15.3'
    integrationTestImplementation 'org.spockframework:spock-core:2.0-groovy-3.0'
    integrationTestImplementation 'org.spockframework:spock-spring:2.0-groovy-3.0'
    integrationTestImplementation 'org.spockframework:spock-junit4:2.0-groovy-3.0'
    integrationTestImplementation 'io.rest-assured:rest-assured:4.4.0'
    integrationTestImplementation 'org.assertj:assertj-core:3.19.0'
    integrationTestImplementation(testFixtures(project)) // Fixture가 필요한 경우
}
```

### JaCoCo 사용 시 분리된 테스트의 Coverage를 통합하는 방법

이미 Unit Test(`src/test`)에 JaCoCo를 사용하여 커버리지를 측정하고 있었다면, integration test를 적용한 이후에 리포트를 생성해보면 integration test의 커버리지가 반영이 되지 않는 문제를 발견할 수 있을 것이다.

JaCoCo가 리포트를 생성할 때는 IntelliJ 기준 `build/jacoco/*.exec` 파일을 참고하는데, 따로 지정하지 않으면 `text.exec` 만을 사용해
리포트를 생성하고 Violation Rule을 검증한다.

이 작업을 integration test에도 적용하려면 아래처럼 `jacocoTestReport` task와 `jacocoTestCoverageVerification`에 모든 `.exec` 파일을
참조하겠다는 내용을 써주면 된다.

```groovy
tasks.jacocoTestReport {
    // integration test 실행 결과 포함
    executionData(fileTree(project.buildDir).include("jacoco/*.exec"))

    reports {
        csv.enabled(true)
    }
}

jacocoTestCoverageVerification {
  // integration test 실행 결과 포함
  executionData(fileTree(project.buildDir).include("jacoco/*.exec"))

  violationRules {
    rule {
        element = 'CLASS'
        limit {
            counter = 'BRANCH'
            minimum = 1.0
        }
    }
    rule {
        element = 'CLASS'
        limit {
            counter = 'LINE'
            minimum = 1.0
        }
    }
}
```

## References

- [Test Fixture 추가 방법 - Gradle 문서: Testing in Java & JVM projects](https://docs.gradle.org/current/userguide/java_testing.html#sec:java_test_fixtures)
- [integration/functional test 정의 방법 - Gradle 문서: Testing Gradle plugin](https://docs.gradle.org/current/userguide/testing_gradle_plugins.html)
